// This is the MAGMA code used to compute the two-torsion subgroup of the modular Jacobin J0(63) 

KK<U> := PolynomialRing(Rationals());
f := U^8 - 9*U^7 + 123*U^6 + 54*U^5 + 861*U^4 - 5130*U^3 + 11250*U^2 - 9882*U + 3357;

// all our quadritangents are defined over the following number field 

K := NumberField(f) ;             
OK := Integers(K) ;
PP := PrimesUpTo(50,K) ;

P := PP[4] ;
// P is a prime ideal of norm 5^2 and we'll use this prime in our reduction map 

R<[x]> := PolynomialRing(Rationals(),5) ;
f1 := x[1]*x[3] - x[2]^2 + x[2]*x[5] - x[3]*x[4] - x[5]^2 ;
f2 := x[1]*x[4] - x[2]*x[3] - x[3]*x[5];
f3 := x[1]*x[5] - x[2]*x[4] - x[3]^2;
P2:=ProjectiveSpace(R);
X:=Curve(P2,[f1,f2,f3]);
F25,pi := ResidueClassField(P) ;
X25 := ChangeRing(X,F25);
Cl25, phi,psi := ClassGroup(X25) ;
Z := FreeAbelianGroup(1);
degr := hom<Cl25 -> Z | [ Degree(phi(g)) : g in OrderedGenerators(Cl25)]>;
J25 := Kernel(degr) ;

// J25 is the set J0(63)(F_25) and since reduction is injective on the torsion, we'll carry out all of  computations in this space 

// we define the two-torsion subgroup defined by our quadritangents 

R := Roots(f, K) ;
Zu<u> := PolynomialRing(K) ;
S1 := (1/2786270549625)*(2044809443*u^7 - 15025064559*u^6 + 224883862375*u^5 + 493919927172*u^4 + 2380053868785*u^3 - 7716538135980*u^2 +
    9069689105670*u - 1191262726656);
S2 := (1/2786270549625)*(176568248*u^7 - 1726825864*u^6 + 22918922030*u^5 - 3947316978*u^4 + 70303651005*u^3 - 978826371060*u^2 + 
    1178709663390*u - 389785345146);
S3 := (-1/2786270549625)*(449735777*u^7 - 5129226196*u^6 + 62219275440*u^5 - 93445474752*u^4 + 83692695300*u^3 - 4192809849660*u^2 + 
    8753844362040*u- 4643674951824);
coeff1 := [ [-1, R[i,1], Evaluate(S1, R[i,1]), Evaluate(S2, R[i,1]), Evaluate(S3, R[i,1]) ] : i in [1..#R] ] ;

g4 := U^8 + 18*U^7 + 18*U^6 - 702*U^5 - 147*U^4 + 3942*U^3 + 5643*U^2 + 3321*U +3357;
R := Roots(g4, K) ;
Zu<u> := PolynomialRing(K) ;
S1 := (1/265433175)*(71715*u^7 + 1116438*u^6 - 1641154*u^5 - 50781600*u^4 + 105064083*u^3 + 133565706*u^2 - 123317013*u - 88757019);
S2 := (-1/265433175)*(21776*u^7 + 427310*u^6 + 935126*u^5 - 12576264*u^4 + 6934875*u^3 + 4148430*u^2 + 1297194*u - 356004);
S3 := (-1/265433175)*(49939*u^7 + 689128*u^6 - 2576280*u^5 - 38205336*u^4 + 98129208*u^3 + 129417276*u^2 + 140818968*u + 177032160);

coeff4 := [ [-1, R[i,1], Evaluate(S1, R[i,1]), Evaluate(S2, R[i,1]), Evaluate(S3, R[i,1]) ] : i in [1..#R] ] ;
coeff := coeff1 cat coeff4 ;
C := [ [ OK ! a : a in b ] : b in coeff ] ;

// we reduce all our equations modulo P and define the equations of the planes over F_25

C := [ [pi(a) : a in c ] : c in C ];
n := #C ;
ZX<[X]> := PolynomialRing(F25,5) ;
p := [] ;
for i in [1..n] do ;
p[i] := &+[C[i][j]*X[j] : j in [1..5] ] ;
end for ; 
QT := p ;
F1 := Evaluate(f1, X) ;
F2 := Evaluate(f2, X) ;
F3 := Evaluate(f3, X) ;

// we compute the 2-torsion points defined by our quadritangent planes over F_25
Cl1 := [] ;
for i in [1..#QT] do ;
a := QT[i] ;
Cl := Cluster(ProjectiveSpace(ZX),[F1, F2, F3, a ] ) ;
Cl1[i] := Cl ;
end for ;
St := [ [Eltseq(a) : a in Points(Cl1[i])] : i in [1..#Cl1] ] ;
StX25 := [ [X25 ! a : a in b ] : b in St];
StX25 := [ [ Place(a) : a in b] : b in StX25];
ICl1 := [ IrreducibleComponents(a) : a in Cl1]; 
DICl1  := [ [Degree(a) : a in b] : b in ICl1 ];
DICl1 := [ [1/2*c : c in b ] : b in DICl1 ] ;
DICl1 := [ [ Integers() ! a : a in b ] : b in DICl1 ];
divs := [] ;
n := #StX25 ;

for i in [1..n] do ;
a := StX25[i] ;
b := DICl1[i] ;
c := [ b[j]*a[j] : j in [1..#a] ] ;
c := &+c ;
divs[i] := c ;
end for ;

// the resulting two-torsion subgroup is generated by the following set 

DD := [ divs[i] - divs[1] : i in [2..#divs] ] ;
H1 := [ psi(a) : a in DD ];   

// we now find the 2-torsion subgroup of the cuspidal subgroup of J0(63) 

// we being by defining the cups on our model of the curve, there are 4 rational cusps, 4 quadratic cusps

Cu := [ [2,1,1,1,1], [0,0,0,1,0],[-1,1,1,-2,1],[1,0,0,0,0]];
Cu := [ X25 ! a : a in Cu] ;
Cu := [ Place(a) : a in Cu] ;

c1 := 8*U^2 -4*U + 2;
R1 := Roots(c1,K) ;
t1 := [ [2*R1[i,1], 1, 2*R1[i,1] -1, 4*R1[i,1], 1 ] :  i in [1..2] ] ;
t1 := [ [ OK ! a : a in b ] : b in t1];
t1 := [ [ pi(a) : a in b ] : b in t1 ];
t1X25 := [ X25 ! a : a in t1 ];
t1pX25 := [ Place(b) : b in t1X25 ];

c2 := 2*u^2 +2*u + 2 ;
R2 := Roots(c2,K) ;
t2 := [ [2*R2[i,1], 1, -R2[i,1] -1, R2[i,1], 1 ] :  i in [1..2] ] ;
t2 := [ [ OK ! a : a in b ] : b in t2];
t2 := [ [ pi(a) : a in b ] : b in t2 ];
t2X25 := [ X25 ! a : a in t2 ];
t2pX25 := [ Place(b) : b in t2X25 ];
CU := Cu cat t1pX25 cat t2pX25 ;
Divs := [ CU[i] - CU[1] : i in [2..#CU] ] ;

// two-torsion part of the group defined by the above 

t1 := -3*Divs[1] + 3*Divs[3] ;
t2 := 6*Divs[1] -14*Divs[2] - 10*Divs[5] + 14*Divs[7] ;
t3 := -3*Divs[1] + Divs[2] - Divs[3] - 4*Divs[4] + 4*Divs[5] ;
t4 := - Divs[1] - Divs[2] + Divs[3] ;

t22 := 6*Divs[1] -14*Divs[2] - 10*Divs[4] + 14*Divs[6] ;
t33 := -3*Divs[1] + Divs[2] - Divs[3] - 4*Divs[5] + 4*Divs[4] ;
DDivs := [ t1, t2, t3, t4,t22, t33] ;               
H2 := [ psi(a) : a in DDivs];


// we compute the 2-torsion subgroup generated by cuspidal divisors of order 2 and the quadritangents computed 

H := H1 cat H2 ; 
ZN := FreeAbelianGroup(#H);
hh := hom< ZN -> J25 | [ a : a in H] >;
ihh := Image(hh) ;

print "The 2-torsion subgroup generated by cuspidal divisors of order 2 and the quadritangents computed is:";

ihh; 

assert Order(ihh) eq 2^10;

print "Thus we have compute the entire 2-torsion subgroup of the Jacobian of X0(63).";


// we now take Galois invariants to compute the rational part of the two-torsion subgroup

// The automorphism group of K has two generators

A,b,c := AutomorphismGroup(K) ;
s1 := c(A.1) ;
s2 := c(A.2) ;


// Using the above generators, we explicitly compute their action on the roots of the defining polynomials of the orbits of quadritangents, and hence the Galois action on the two-torsion 

// The action of s1 on the generators of the two-torsion subgroup is represented by the following 

cpt1 := [ ZN.2 - ZN.6, -ZN.6, ZN.5 - ZN.6, ZN.7 - ZN.6, ZN.4 - ZN.6, ZN.1 - ZN.6 , ZN.3 - ZN.6, ZN.10 - ZN.6, ZN.13 - ZN.6, ZN.12 - ZN.6, ZN.14 - ZN.6 , ZN.15 - ZN.6, ZN.11 - ZN.6, ZN.9 - ZN.6, ZN.8 - ZN.6, ZN.16, ZN.20, ZN.21, ZN.19, ZN.17, ZN.18] ;

// we compute the subgroup fixed by the action of s1

conj1 := hom< ZN -> ZN | cpt1>;
mu := hom< ZN -> J25 | [ hh(ZN.i) - hh(conj1(ZN.i)) : i in [1..21]]>;
ker1 := Kernel(mu);
imKer1 := sub<J25 | [hh(k) : k in Generators(ker1)]>;


// The action of s2 on the generators of two-torsion subgroup is represented by the following vector 
cpt2 := [ ZN.3 - ZN.4, ZN.7 - ZN.4, ZN.1 - ZN.4, - ZN.4, ZN.6 - ZN.4, ZN.5 - ZN.4,  ZN.2 - ZN.4, ZN.13 - ZN.4, ZN.10 - ZN.4, ZN.9 - ZN.4, ZN.15 - ZN.4, ZN.14 - ZN.4, ZN.8 - ZN.4, ZN.12 - ZN.4, ZN.11 - ZN.4, ZN.16, ZN.17, ZN.18, ZN.19, ZN.20, ZN.21] ;

// the subgroup fixed by s2 is:
conj2 := hom< ZN -> ZN | cpt2>;
mu := hom< ZN -> J25 | [ hh(ZN.i) - hh(conj2(ZN.i)) : i in [1..21]]>;
ker2 := Kernel(mu);
imKer2 := sub<J25 | [hh(k) : k in Generators(ker2)]>;


// The intersection of the two fixed subgroups is the rational two-torsion subgroup 

I := imKer1 meet imKer2;

print  "The rational 2-torsion subgroup is:";
I ;

print " Thus we have verified the Generalised Ogg conjecture for N=63.";
