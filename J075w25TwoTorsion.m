// This is the MAGMA code used to compute the 2-torsion subgroup of the Jacobian of the curve X0(75)/w25 
// We use the bitangents found using X075w25Bitangents.m

// We begin by giving the equations of the bitangents

Zu<u> := PolynomialRing(Rationals()) ;

g := u^6 +  14*u^5 - 5*u^4 - 20*u^3 + 95*u^2 + 134*u - 139;
K := SplittingField(g) ;
// all bitangents are defined over the above field 

g1 := g ;
g12 := 1/9164*(41*u^5 + 467*u^4 - 1312*u^3 + 2604*u^2 + 2687*u -3083);

g2 :=  -u^6 - 3*u^5 + 5*u^3 - 60*u^2 - 63*u + 41 ;
g22 := 1/27*( u^5 + u^4 - 2*u^3 - u^2 + 62*u - 34) ; 

g3 := 11*u^6 + 49*u^5 + 20*u^4 - 55*u^3 + 40*u^2 - 11*u + 1 ;
g32 := 1/17*( 407*u^5 + 1901*u^4 + 1154*u^3 - 1773*u^2 + 1106*u -188) ;

R1 := Roots(g1,K);
PT1 := [ [R1[i,1],Evaluate(g12,R1[i,1])] : i in [1..6] ];
R2 := Roots(g2,K);
PT2 := [ [R2[i,1],Evaluate(g22,R2[i,1])] : i in [1..6] ];
R3 := Roots(g3,K);
PT3 := [ [R3[i,1],Evaluate(g32,R3[i,1])] : i in [1..6] ];

g4 := -u^2 - u + 1 ;
g42 := 3*u - 2 ; 
g5 := u^2 - 7*u + 11 ;
g52 := -u + 2 ;
g6 := -u^2 - 8*u + 4 ;
g62 := 2*u - 1 ;
R4 := Roots(g4,K);
PT4 := [ [R4[i,1],Evaluate(g42,R4[i,1])] : i in [1..2] ];
R5 := Roots(g5,K);
PT5 := [ [R5[i,1],Evaluate(g52,R5[i,1])] : i in [1..2] ];
R6 := Roots(g6,K);
PT6 := [ [R6[i,1],Evaluate(g62,R6[i,1])] : i in [1..2] ];

PT7 := [ [1,-1], [2,-1], [-3,-1], [-2,-1] ];

BPT := PT1 cat PT2 cat PT3 cat PT4 cat PT5 cat PT6 cat PT7 ;
BF<[x]> := PolynomialRing(K,3);
Bbtan := [ x[1] - a[1]*x[2] -a[2]*x[3] : a in BPT ];
CBtan := [ Coefficients(a) : a in Bbtan ];
DenomCBtan := [ [Denominator(s) : s in a ] : a in CBtan ];
LCmBtan := [ Lcm(s[2],s[3]) : s in DenomCBtan ]; 
Btan := [ LCmBtan[i]*Bbtan[i] : i in [1..28] ];


// we now clear denominators and scale appropriately to ensure that all our equations are defined over the ring of integers OK of K 

OK := Integers(K) ;
P := 289*OK ;
p1 := Factorization(P)[1,1] ; // a prime ideal of norm 289 

R<[x]> := PolynomialRing(K,3);
eqn:= 3*x[1]^3*x[3] - 3*x[1]^2*x[2]^2 + 5*x[1]^2*x[3]^2 - 3*x[1]*x[2]^3 - 19*x[1]*x[2]^2*x[3] - x[1]*x[2]*x[3]^2 + 3*x[1]*x[3]^3 + 2*x[2]^4 +7*x[2]^3*x[3] - 7*x[2]^2*x[3]^2 - 3*x[2]*x[3]^3; 

P2:=ProjectiveSpace(R);
X:=Curve(P2,eqn);

// we reduce our curve modulo p1
F289,pi := ResidueClassField(p1) ;
X289 := ChangeRing(X,F289);
Cl289, phi,psi := ClassGroup(X289) ; 

CBtan := [ Coefficients(a) : a in Btan];
ACBta := [ [ OK ! s : s in a ] : a in CBtan ];
F289CBtan := [ [pi(s) : s in a ] : a in ACBta];


BB<[v]> := PolynomialRing(F289,3);
F289Btan := [ a[1]*v[1] + a[2]*v[2] + a[3]*v[3] : a in F289CBtan ];
// above we reduce the equations defining the bitangents modulo p1

FX289 := FunctionField(X289);
FXBtan := [ Evaluate(a, [FX289.1, FX289.2, 1 ]) : a in F289Btan];
Db := [ Divisor(FXBtan[27]/FXBtan[i]) : i in [1..28] ];
DDb := [Decomposition(s) : s in Db];
DDb1 := [ [1/2*a[i,2] : i in [1..#a]] : a in DDb ];
DDb1 := [ [Integers()!s : s in a ] : a in DDb1] ;
DDb2 := [ [a[i,1] : i in [1..#a] ] : a in DDb];
divs := [ [DDb1[i][j]*DDb2[i][j] : j in [1..#DDb1[i]]] : i in [1..#DDb1] ]; 
divs := [ &+a : a in divs ];



Z := FreeAbelianGroup(1);
degr := hom<Cl289 -> Z | [ Degree(phi(g)) : g in OrderedGenerators(Cl289)]>;
J289 := Kernel(degr) ;
H := [psi(a) : a in divs ];
H := [ J289!s : s in H ]; 
H := [ s : s in H | s ne J289!0];
ZN := FreeAbelianGroup(#H);
h := hom<ZN -> J289 | [a : a in H] > ;

im := Image(h) ;

assert #im eq 2^6 ;


print "We have computed the entire 2-torsion subgroup of the Jacobian, since the  subgroup generated by the bitangents computed is:";
im ;

// we now take Galois invariants to compute the rational 2-torsion subgroup 

// we compute the  automorphism group of K and find that it has two generators.


A,b,piK := AutomorphismGroup(K); 
s1 := piK(A.1);
s2 := piK(A.2);

// we compute the actions of these on the bitangents ( by computing their action on the coefficients ) and find that the first generator acts on the generators of the 2-torsion subgroup via:

cpt := [ZN.4,ZN.6,ZN.5,ZN.1,ZN.3,ZN.2,ZN.8,ZN.7,ZN.10,ZN.9,ZN.12,ZN.11,ZN.14,ZN.13,ZN.17,ZN.18,ZN.15,ZN.16,ZN.20,ZN.19,ZN.22,ZN.21,ZN.24,ZN.23,ZN.25,ZN.26,ZN.27];

// taking taking invariants:

conj := hom< ZN -> ZN | cpt>;
mu := hom< ZN -> J289 | [ h(ZN.i) - h(conj(ZN.i)) : i in [1..27]]>;
ker := Kernel(mu);
imKer := sub<J289 | [h(k) : k in Generators(ker)]>; 

// the second geneators acts via:

cpt2 := [ ZN.3, ZN.1, ZN.5, ZN.6, ZN.4, ZN.2, ZN.11, ZN.10, ZN.7, ZN.9, ZN.12, ZN.8, ZN.17, ZN.18, ZN.14, ZN.13, ZN.15, ZN.16, ZN.20, ZN.19, ZN.22, ZN.21, ZN.24, ZN.23, ZN.25, ZN.26, ZN.27];

// we take Galois invariants 

conj2 := hom< ZN -> ZN | cpt2>;
mu2 := hom< ZN -> J289 | [ h(ZN.i) - h(conj2(ZN.i)) : i in [1..27]]>;
ker2 := Kernel(mu2);
imKer2 := sub<J289 | [h(k) : k in Generators(ker2)]>;

// hence the rational 2-torsion subgroup is the intersection of the two 

rat2tors := imKer meet imKer2;

print " The rational 2-torsion subgroup of J is:";

rat2tors ;

// we verfiy that the first 3 stated rational bitangents are sufficient to generate the rational 2-torsion subgroup 


// the following are the divisors resulting from the intesection of  x - y + z/x + 3y + z   and x - 2y +z/x + 3y + z with the curve, and reducing modulo p 
a1 := divs[25] ;
a2 := divs[26] ;

H := [ a1, a2] ;
 H := [ psi(a) : a in H ] ;
H := [ J289 ! s : s in H ];
ZN := FreeAbelianGroup(#H) ;
h := hom< ZN -> J289 | [ a : a in H ] >;

rh := Image(h) ;

assert #rh eq #rat2tors ;

print "The rational 2-torsion subgroup is generated by the difference of the divisors obtained from the intersection of the curve with the bitangents defined by x - y  + z, x - 2y + z, x + 3y + z. ";
